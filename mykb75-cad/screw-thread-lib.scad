/*  Screw Thread Lib
    Michael Murray 2015
    
    *** OpenSCAD must be setup for angles in degrees and lengths in mm ***
    
    Metric or imperial threads can be generated by setting the unit parameter
    and using mm or inches when specifying the other parameters.

    Call by: thread(dia,pitch,length,form,angle,inner,outer,step,unit)
    
    Mandatory Parameters
    --------------------
    dia     nominal diameter of thread
    pitch   thread pitch
    length  length of thread
    
    Optional Parameters
    -------------------
    form    thread form [default = "screw"]
            Values ["screw", "trapezoid", "buttress1","buttress2","buttress3", "round"].
            "screw" is a normal triangular thread
            "trapezoid" is the metric version of the acme thread
            "buttress1" is a simple buttress with truncations at 1/8 pitch
            "buttress2" is a simple buttress with truncations at 1/6 pitch
            "buttress3" is the 45/5 buttress with truncations at 1/8 pitch
            "round" is a profile for plastic screws for toys etc. Its overhang
            is limited to 45 degrees so it can be 3D printed without supports            
    angle   thread pressure angle [default = 30]
            ONLY EFFECTIVE WHEN form = "screw" or "trapezoid".
    inner   diameter at the base of the thread [default is ISO standard]
            ONLY EFFECTIVE WHEN form = "screw" or "trapezoid".
    outer   outer diameter of the thread [default is ISO standard]
            ONLY EFFECTIVE WHEN form = "screw" or "trapezoid".
    step    rotation for each iteration when generating the thread.
            (smaller step = finer detail) [default = 5 degrees]
            Use a POSITIVE value for RH threads & a NEGATIVE value for LH threads.
    unit    values are "mm" (default) or "inch"
    
    NOTES:
    This program compiles quickly with f5, but is very slow to render 
    with f6, so advise rendering only when stl export is needed.
    
    The round profile takes MUCH longer than the others.
    
    To create an ACME thread use: form="trapezoid" & angle=29.
    
*/

module screwProfile(dia,pitch,angle,arcLen,depth)
{
    r = dia/2;
    p = pitch/2;
    L = r-depth;
    points=[[0,pitch],[0,0],[L,0],[r,p],[L,pitch]];
    
    translate([0,arcLen/2,0]) 
    rotate([90,0,0])
        linear_extrude(height = arcLen, center = false)
            polygon(points, convexity=4);
}

module buttressProfile(dia,pitch,arcLen) // simple butress
{
    r = dia/2;
    L = r-pitch;
    points=[[0,pitch],[0,0],[L,0],[r,pitch]];
    
    translate([0,arcLen/2,0]) 
    rotate([90,0,0])
        linear_extrude(height = arcLen, center = false)
            polygon(points, convexity=4);
}

module buttress3Profile(dia,pitch,arcLen) // the butress 45-5 profile
{
    r = dia/2;
    L = r-pitch;
    points=[[0,pitch],[0,0],[L,0],[r,pitch*0.94],[L,pitch]];
    
    translate([0,arcLen/2,0]) 
    rotate([90,0,0])
        linear_extrude(height = arcLen, center = false)
            polygon(points, convexity=4);
}

module arcSeg(r,arcLen) // used by module roundProfile
{
    translate([-r,0,0])
    difference()
    {
        translate([r/2,0,r/2])
        rotate([-90,0,0])
            cylinder(d=1.414*r, h=arcLen, $fn=r*48);
        translate([-r,-1,-0.5*r])
            cube([2*r,arcLen+2,2*r]);
    }
}

module roundProfile(dia,pitch,arcLen)
{
    r = dia/2;
    L = r-pitch*0.1768;
    
    translate([0,-arcLen/2,0]) 
    union()
    {
        // lower part
        cube([L,arcLen,pitch/2]); // thread body
        translate([L,0,0])
            arcSeg(pitch/2,arcLen); // add curve
        
        // upper part
        difference()
        {
            translate([0,0,pitch/2])
                cube([L,arcLen,pitch/2]); // thread body
            translate([L,arcLen+1,pitch/2])
            rotate([0,0,180])
                arcSeg(pitch/2,arcLen+2); // remove curve
        }
    }
}

module threadForm(dia,pitch,len,form,angle,step,depth) // create the basic thread
{
    length = len+2*pitch;
    
    // variables to approximate the profile
    // ------------------------------------
    arcLen = PI*dia*step/360;
    totalSteps = floor((360/step)*(length/pitch)); // number of steps
    z = pitch*step/360;
    
    translate([0,0,-pitch])
    for (i = [0 : totalSteps])
    {
        translate([0,0,i*z])
        rotate([0,0,i*step])
        if(form=="screw"||form=="trapezoid")
            screwProfile(dia,pitch,angle,arcLen,depth);
        else if(form=="buttress1"||form=="buttress2")
            buttressProfile(dia,pitch,arcLen);
        else if(form=="buttress3")
            buttress3Profile(dia,pitch,arcLen);
        else
            roundProfile(dia,pitch,arcLen);
    }
}

module makeThread(dia,pitch,length,form,angle,inner,outer,step=5)
{
    // call the threadForm module and add root & tip truncations
    
    // set thread defaults
    // -------------------
    angle = angle==undef ? 30 : angle;
    td = pitch*cos(angle); // thread depth
    // thread root truncation
    inner = form=="round" ? dia-0.6464*pitch
          : form=="buttress1" ? dia-pitch*7/4
          : form=="buttress2" ? dia-pitch*5/3
          : form=="buttress3" ? dia-pitch*1.63
          : inner==undef ? dia-1.5*td // screw or trapezoid
          : inner; //special size for screw or trapezoid
    // thread tip truncation
    outer = form=="round" ? dia
          : form=="buttress1"||form=="buttress3" ? dia-pitch/4
          : form=="buttress2" ? dia-pitch/3
          : outer==undef&&form=="trapezoid" ? dia-td/2
          : outer==undef&&form=="screw" ? dia-td/4 
          : outer; // special size for screw or trapezoid
    
    intersection()
    {
        union()
        {
            threadForm(dia,pitch,length,form,angle,step,td); // create the thread
            cylinder(d=inner, h=length+pitch,$fn=dia*4); // cut off at thread base
        }
        cylinder(d=outer,h=length+pitch,$fn=dia*4); // cut off thread crests
    }
}

/**** MAIN MODULE ****/

module thread(dia,pitch,length,form="screw",angle=30,inner,outer,step=5,unit="mm")
{
    // parameter checks
    if(dia==undef||pitch==undef||length==undef)
    {
        echo("**** The thread diameter, pitch and length MUST be specified ****");
    }
    else if(!(form=="screw"||form=="trapezoid"||form=="buttress1"||
            form=="buttress2"||form=="buttress3"||form=="round"||form==undef))
    {
        echo("**** INVALID THREAD FORM ****");
    }
    else // convert imperial to metric and make the thread
    {
        dia = unit=="inch" ? dia*25.4 : dia;
        length = unit=="inch" ? length*25.4 : length;
        inner = unit=="inch" ? inner*25.4 : inner;
        outer = unit=="inch" ? outer*25.4 : outer;
        makeThread(dia,pitch,length,form,angle,inner,outer,step);
    }
}

